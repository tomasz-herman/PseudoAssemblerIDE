plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
    id 'org.beryx.runtime' version '1.11.2'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo1.maven.org/maven2')
    }

    maven {
        url = uri('https://maven.pkg.github.com/tomasz-herman/PseudoAssemblerEmulator')
    }
}

dependencies {
    implementation 'com.fifesoft:rsyntaxtextarea:3.1.0'
    implementation 'com.formdev:flatlaf:0.38'
    implementation 'com.hermant:pseudo-assembler-emulator:3.1.6'
    implementation 'org.jetbrains:annotations:19.0.0'
    implementation 'com.fifesoft:autocomplete:3.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
}

group = 'com.paide'
version = '1.0-SNAPSHOT'
sourceCompatibility = '11'

application {
    mainClass = 'com.paide.Main'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        imageName = "Pseudo-Assembler IDE"
        installerName = "Pseudo-Assembler IDE"
        installerOptions = []
        installerType = project.findProperty('installerType')
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/paide.ico']
            installerOptions += [
                    '--win-per-user-install',
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut',
                    '--win-menu-group', 'Pseudo-Assembler',
                    '--name', 'Pseudo-Assembler IDE'
            ]
        }
        if (installerType == 'pkg') {
            imageOptions += ['--icon', 'src/main/resources/paide256.png']
            installerOptions += []
        }
        if (installerType == 'deb') {
            imageOptions += ['--icon', 'src/main/resources/paide256.png']
            installerOptions += [
                    '--linux-menu-group', 'Development',
                    '--linux-shortcut'
            ]
        }
        if (installerType == 'rpm') {
            imageOptions += ['--icon', 'src/main/resources/paide256.png']
            installerOptions += [
                    '--linux-menu-group', 'Development',
                    '--linux-shortcut'
            ]
        }
    }
}

jpackage {
    doFirst {
        project.getProperty('installerType')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
